apply plugin: 'com.android.application'

android {

    defaultConfig {
        applicationId "com.forrestguice.suntimes.solunar"
        minSdkVersion 14
        //noinspection OldTargetApi
        targetSdkVersion 33
        compileSdk 33

        versionCode 3
        versionName "0.2.1"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        resConfigs "en"

        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        manifestPlaceholders.suntimesApplicationId = "com.forrestguice.suntimeswidget"
        manifestPlaceholders.suntimesPermissionRoot = "suntimes"
        def authorityRoot = "solunarperiods.calculator"
        manifestPlaceholders.solunarPeriodsAuthorityRoot = "$authorityRoot"
        buildConfigField "String", "AUTHORITY_ROOT", "\"$authorityRoot\""

        missingDimensionStrategy 'version', 'androidx'
        missingDimensionStrategy 'api', 'api33'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = [ "versiontype" ]
    productFlavors {
        production {
            dimension "versiontype"
            getIsDefault().set(true)
        }
        nightly {
            dimension "versiontype"
            applicationIdSuffix ".nightly"
            versionNameSuffix "-nightly"
            def nightlyVersionCode = new Date().format('yyyyMMddHH') as int
            versionCode nightlyVersionCode

            def authorityRoot = "solunarperiods.calculator.nightly"
            manifestPlaceholders.suntimesApplicationId = "com.forrestguice.suntimeswidget"
            manifestPlaceholders.suntimesPermissionRoot = "suntimes"
            manifestPlaceholders.solunarPeriodsAuthorityRoot = "$authorityRoot"
            buildConfigField "String", "AUTHORITY_ROOT", "\"$authorityRoot\""
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.6.1'                       // 1.7.0 (2024-05-29) requires Java 17, minapi 21+
    //noinspection GradleDependency
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'         // 2.2.0 (2024-10-30) requires Java 17, minapi 21+
    implementation "androidx.preference:preference:1.2.1"
    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //noinspection GradleDependency
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'             // 1.2.0 requires Java 17, minapi 19+
    implementation project(path: ":SuntimesAddon")  //implementation 'com.github.forrestguice:SuntimesAddon:v0.4.0'

    testImplementation 'junit:junit:4.13.2'
    //noinspection GradleDependency
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'                 // 1.2.0 requires minapi 19+
    //noinspection GradleDependency
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'    // 3.6.0 requires minapi 19+
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def getGitHash()
{
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}